EVALUATE

-- Constants --
VAR __BITTABLE = GENERATESERIES ( 1, 2 )
VAR __BASE = 16 -- Hexadecimal

-- Conversion settings --
VAR _Red = 0
VAR _Green = 0
VAR _Blue = 0
VAR _IncreaseOrDecrease = 0 -- 0 = Increase, 1 = Decrease
VAR _MaxGradient = 255
VAR _MinGradient = 50
VAR _GradientSteps = 100 -- Loops Starts At 0

-- To execute --
VAR _SafeMaxGradient = IF ( _MaxGradient > 255, 255, _MaxGradient )
VAR _SafeMinGradient = IF ( _MinGradient < 0, 0, _MinGradient )
VAR _Gradient = IF ( _IncreaseOrDecrease = 0, _SafeMaxGradient, _SafeMinGradient ) 
VAR _RedMultiplier = DIVIDE ( _Gradient - _Red, _GradientSteps, 0 )
VAR _GreenMultiplier = DIVIDE ( _Gradient - _Green, _GradientSteps, 0 )
VAR _BlueMultiplier = DIVIDE ( _Gradient - _Blue, _GradientSteps, 0 )
VAR _Loop = SELECTCOLUMNS ( GENERATESERIES ( 0, _GradientSteps ), "Index", [Value] )
VAR _ColorCodes = 
    ADDCOLUMNS (
        _Loop,
        "RGB", INT ( ( _Red + [Index] * _RedMultiplier ) ) & ", " & INT ( ( _Green + [Index] * _GreenMultiplier ) ) & ", " & INT ( ( _Blue + [Index] * _BlueMultiplier ) ),
        "Hex", "#" &
        CONCATENATEX (
        	__BITTABLE,
        	VAR c = MOD ( TRUNC ( ( _Red + [Index] * _RedMultiplier ) / POWER ( __BASE, [value] - 1 ) ),__BASE )
        	RETURN SWITCH (
        		c,
        		10, "A",
        		11, "B", 
            	12, "C", 
            	13, "D",
            	14, "E",
            	15, "F", 
            	c
        	), ,
        	[value],
        	DESC
        ) & CONCATENATEX (
        	__BITTABLE,
        	VAR c = MOD ( TRUNC ( ( _Green + [Index] * _GreenMultiplier ) / POWER ( __BASE, [value] - 1 ) ),__BASE )
        	RETURN SWITCH (
        		c,
        		10, "A",
        		11, "B", 
            	12, "C", 
            	13, "D",
            	14, "E",
            	15, "F", 
            	c
        	), ,
        	[value],
        	DESC
        ) & CONCATENATEX (
        	__BITTABLE,
        	VAR c = MOD ( TRUNC ( ( _Blue + [Index] * _BlueMultiplier ) / POWER ( __BASE, [value] - 1 ) ),__BASE )
        	RETURN SWITCH (
        		c,
        		10, "A",
        		11, "B", 
            	12, "C", 
            	13, "D",
            	14, "E",
            	15, "F", 
            	c
        	), ,
        	[value],
        	DESC
        )
    )
 RETURN 
 	_ColorCodes